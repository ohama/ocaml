<html><head><meta http-equiv="Content-Type" content="text/html; charset=ISO-8859-1"><title>A Working Example</title><meta name="generator" content="DocBook XSL Stylesheets V1.61.2"><link rel="home" href="index.html" title="GTK+ 2.0 Tree View Tutorial using Ocaml"><link rel="up" href="ch05.html" title="Chapter 5. Mapping Data to the Screen: GTree.view_column and GTree.cell_renderer_*"><link rel="previous" href="ch05s05.html" title="GTree.cell_renderer_text, UTF8, and pango markup"><link rel="next" href="ch05s07.html" title="How to Make a Whole Row Bold or Coloured"></head><body bgcolor="white" text="black" link="#0000FF" vlink="#840084" alink="#0000FF"><div class="navheader"><table width="100%" summary="Navigation header"><tr><th colspan="3" align="center">A Working Example</th></tr><tr><td width="20%" align="left"><a accesskey="p" href="ch05s05.html">Prev</a> </td><th width="60%" align="center">Chapter 5. Mapping Data to the Screen: GTree.view_column and GTree.cell_renderer_*</th><td width="20%" align="right"> <a accesskey="n" href="ch05s07.html">Next</a></td></tr></table><hr></div><div class="sect1" lang="en"><div class="titlepage"><div><div><h2 class="title" style="clear: both"><a name="sec-treeview-col-example"></a>A Working Example</h2></div></div><div></div></div><p>
Here is our example from the very beginning again (with an additional column though),
only that the contents of the model are rendered properly on the screen this time.
Both attributes and a cell data function are used for demonstration purposes.
</p><pre class="programlisting">
(* file: name_born.ml *)

open Gobject.Data

let cols = new GTree.column_list
let col_first_name = cols#add string
let col_last_name = cols#add string
let col_year_born = cols#add uint

let create_and_fill_model data =
  let treestore = GTree.tree_store cols in
  let append (first_name, last_name, born) =
    let toplevel = treestore#append () in
    treestore#set ~row:toplevel ~column:col_first_name first_name;
    treestore#set ~row:toplevel ~column:col_last_name last_name;
    if born &gt; 0
    then treestore#set ~row:toplevel ~column:col_year_born born
  in
  List.iter append [
    ("Maria", "Incognito", 0);
    ("Jane", "Average", 1962);
    ("Janinita", "Average", 1985)
  ];
  treestore

let age_cell_data_func renderer (model:GTree.model) iter =
  let year_now = 2004 in
  let year_born = model#get ~row:iter ~column:col_year_born in
  if year_born &lt;= year_now &amp;&amp; year_born &gt; 0
  then (
    let age = year_now - year_born in
    renderer#set_properties [
      `TEXT (Printf.sprintf "%u years old" age);
      `FOREGROUND_SET false
    ]
  ) else (
    renderer#set_properties [
      `TEXT "age unknown";
      `FOREGROUND "Red";
      `FOREGROUND_SET true;
    ]
  )
  
let create_view_and_model ~packing () =
  let view = GTree.view ~packing () in

  (* Column #1 *)
  (* pack cell renderer into tree view column *)
  (* connect 'text' property of the cell renderer to
   * model column that contains the first name *)
  let col = GTree.view_column ~title:"First Name"
      ~renderer:(GTree.cell_renderer_text [], ["text", col_first_name]) () in
  (* pack tree view column into tree view *)
  view#append_column col;

  (* Column #2 *)
  (* create cell_renderer and set 'weight' property of it to
   * bold print (we want all last name in bold) *)
  let cell_renderer = GTree.cell_renderer_text [`WEIGHT `BOLD] in
  (* pack cell renderer into tree view column *)
  (* connect 'text' property of the cell renderer to
   * model column that contains the last name *)
  let col = GTree.view_column ~title:"Last Name"
      ~renderer:(cell_renderer, ["text", col_last_name]) () in
  (* pack tree view column into tree view *)
  view#append_column col;

  let renderer = GTree.cell_renderer_text [] in
  (* pack cell renderer into tree view column *)
  let col = GTree.view_column ~title:"Age"
      ~renderer:(renderer, []) () in
  (* connect a cell data function *)
  col#set_cell_data_func renderer (age_cell_data_func renderer);
  (* pack tree view column into tree view *)
  view#append_column col;

  let model = create_and_fill_model () in
  view#set_model (Some (model#coerce));

  view#selection#set_mode `NONE;
  view

let main () =
  let window = GWindow.window ~title:"Treeview" ~border_width:10 () in
  window#connect#destroy ~callback:GMain.Main.quit;
  let view = create_view_and_model ~packing:window#add () in
  window#show ();
  GMain.Main.main ()

let _ = Printexc.print main ()
</pre></div><div class="navfooter"><hr><table width="100%" summary="Navigation footer"><tr><td width="40%" align="left"><a accesskey="p" href="ch05s05.html">Prev</a> </td><td width="20%" align="center"><a accesskey="u" href="ch05.html">Up</a></td><td width="40%" align="right"> <a accesskey="n" href="ch05s07.html">Next</a></td></tr><tr><td width="40%" align="left" valign="top">GTree.cell_renderer_text, UTF8, and pango markup </td><td width="20%" align="center"><a accesskey="h" href="index.html">Home</a></td><td width="40%" align="right" valign="top"> How to Make a Whole Row Bold or Coloured</td></tr></table></div></body></html>
